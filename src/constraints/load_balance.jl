# REoptÂ®, Copyright (c) Alliance for Sustainable Energy, LLC. See also https://github.com/NREL/REopt.jl/blob/master/LICENSE.

function add_elec_load_balance_constraints(m, p; _n="") 

	##Constraint (8a): Electrical Load Balancing with Grid
    if isempty(p.s.electric_tariff.export_bins)
        conrefs = @constraint(m, [ts in p.time_steps_with_grid],
            sum(p.production_factor[t, ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t,ts] for t in p.techs.elec)  
            + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.elec) 
            + sum(m[Symbol("dvGridPurchase"*_n)][ts, tier] for tier in 1:p.s.electric_tariff.n_energy_tiers)
            ==
            sum(sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec) 
                + sum(m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts]) 
                + sum(m[Symbol("dvProductionToCompressor"*_n)][t, ts]) 
                + m[Symbol("dvCurtail"*_n)][t, ts] for t in p.techs.elec)
            + sum(m[Symbol("dvGridToStorage"*_n)][b, ts] for b in p.s.storage.types.elec)
            + m[Symbol("dvGridToElectrolyzer"*_n)][ts]
            + m[Symbol("dvGridToCompressor"*_n)][ts]
            + sum(m[Symbol("dvThermalProduction"*_n)][t, ts] / p.cop[t] for t in p.techs.cooling)
            + sum(m[Symbol("dvThermalProduction"*_n)][t,ts] / p.heating_cop[t] for t in p.techs.electric_heater)
            + p.s.electric_load.loads_kw[ts]
            - p.s.cooling_load.loads_kw_thermal[ts] / p.cop["ExistingChiller"]
            + sum(p.ghp_electric_consumption_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
        )
    else
        conrefs = @constraint(m, [ts in p.time_steps_with_grid],
            sum(p.production_factor[t, ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t,ts] for t in p.techs.elec)
            + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.elec)
            + sum(m[Symbol("dvGridPurchase"*_n)][ts, tier] for tier in 1:p.s.electric_tariff.n_energy_tiers)
            ==
            sum(sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec) 
                + sum(m[Symbol("dvProductionToGrid"*_n)][t, u, ts] for u in p.export_bins_by_tech[t]) 
                + sum(m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts]) 
                + sum(m[Symbol("dvProductionToCompressor"*_n)][t, ts])
                + m[Symbol("dvCurtail"*_n)][t, ts] for t in p.techs.elec)
            + sum(m[Symbol("dvGridToStorage"*_n)][b, ts] for b in p.s.storage.types.elec)
            + m[Symbol("dvGridToElectrolyzer"*_n)][ts]
            + m[Symbol("dvGridToCompressor"*_n)][ts]
            + sum(m[Symbol("dvThermalProduction"*_n)][t, ts] / p.cop[t] for t in p.techs.cooling)
            + sum(m[Symbol("dvThermalProduction"*_n)][t,ts] / p.heating_cop[t] for t in p.techs.electric_heater)
            + p.s.electric_load.loads_kw[ts]
            - p.s.cooling_load.loads_kw_thermal[ts] / p.cop["ExistingChiller"]
            + sum(p.ghp_electric_consumption_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
        )
    end

	for (i, cr) in enumerate(conrefs)
		JuMP.set_name(cr, "con_load_balance"*_n*string("_t", i))
	end
	
	##Constraint (8b): Electrical Load Balancing without Grid
	if !p.s.settings.off_grid_flag # load balancing constraint for grid-connected runs
        @constraint(m, [ts in p.time_steps_without_grid],
            sum(p.production_factor[t,ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t,ts] for t in p.techs.elec)  
            + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.elec)
            ==
            sum(sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec) 
                + sum(m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts]) 
                + sum(m[Symbol("dvProductionToCompressor"*_n)][t, ts])
                + m[Symbol("dvCurtail"*_n)][t, ts] for t in p.techs.elec)
            + p.s.electric_load.critical_loads_kw[ts]
        )
    else # load balancing constraint for off-grid runs - not altering off-grid to include hydrogen
        @constraint(m, [ts in p.time_steps_without_grid],
            sum(p.production_factor[t,ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t,ts] for t in p.techs.elec)
            + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.elec)
            ==
            sum(sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec)
                + m[Symbol("dvCurtail"*_n)][t, ts] for t in p.techs.elec)
            + p.s.electric_load.critical_loads_kw[ts] * m[Symbol("dvOffgridLoadServedFraction"*_n)][ts]
        )
        ##Constraint : For off-grid scenarios, annual load served must be >= minimum percent specified
        @constraint(m, 
            sum(m[Symbol("dvOffgridLoadServedFraction"*_n)][ts] * p.s.electric_load.critical_loads_kw[ts] for ts in p.time_steps_without_grid)
            >=
			sum(p.s.electric_load.critical_loads_kw) * p.s.electric_load.min_load_met_annual_fraction 
		)
    end

end


function add_production_constraints(m, p; _n="")
	# Constraint (4d): Electrical production sent to storage or export must be less than technology's rated production
    if isempty(p.s.electric_tariff.export_bins)
        @constraint(m, [t in p.techs.elec, ts in p.time_steps_with_grid],
            sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec)  
            + m[Symbol("dvCurtail"*_n)][t, ts]
            + m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts] 
            + m[Symbol("dvProductionToCompressor"*_n)][t, ts]
            <= 
            p.production_factor[t, ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t, ts]
        )
    else
        @constraint(m, [t in p.techs.elec, ts in p.time_steps_with_grid],
            sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec)  
            + m[Symbol("dvCurtail"*_n)][t, ts]
            + m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts] 
            + m[Symbol("dvProductionToCompressor"*_n)][t, ts]
            + sum(m[Symbol("dvProductionToGrid"*_n)][t, u, ts] for u in p.export_bins_by_tech[t])
            <= 
            p.production_factor[t, ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t, ts]
        )
    end

	# Constraint (4e): Electrical production sent to storage or curtailed must be less than technology's rated production - no grid
	@constraint(m, [t in p.techs.elec, ts in p.time_steps_without_grid],
        sum(m[Symbol("dvProductionToStorage"*_n)][b, t, ts] for b in p.s.storage.types.elec)
        + m[Symbol("dvProductionToElectrolyzer"*_n)][t, ts] 
        + m[Symbol("dvProductionToCompressor"*_n)][t, ts]
        + m[Symbol("dvCurtail"*_n)][t, ts]  
        <= 
        p.production_factor[t, ts] * p.levelization_factor[t] * m[Symbol("dvRatedProduction"*_n)][t, ts]
	)

end


function add_thermal_load_constraints(m, p; _n="")

    m[Symbol("binFlexHVAC"*_n)] = 0
    m[Symbol("dvTemperature"*_n)] = 0
    m[Symbol("dvComfortLimitViolationCost"*_n)] = 0

    if !isnothing(p.s.flexible_hvac)
        #= FlexibleHVAC does not require equality constraints for thermal loads. The thermal loads
        are instead a function of the energy required to keep the space temperature within the 
        comfort limits.
        =#
        add_flexible_hvac_constraints(m, p, _n=_n) 

	##Constraint (5b): Hot thermal loads
    else    
        if !isempty(p.techs.heating)
            
            if !isempty(p.techs.steam_turbine)
                @constraint(m, [ts in p.time_steps_with_grid],
                    sum(m[Symbol("dvThermalProduction"*_n)][t,ts] for t in union(p.techs.heating, p.techs.chp))
                    + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.hot)
                    + sum(p.ghp_heating_thermal_load_served_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
                    ==
                    (p.s.dhw_load.loads_kw[ts] + p.s.space_heating_load.loads_kw[ts])
                    + sum(m[Symbol("dvProductionToWaste"*_n)][t,ts] for t in p.techs.chp)
                    + sum(m[Symbol("dvProductionToStorage"*_n)][b,t,ts] for b in p.s.storage.types.hot, t in union(p.techs.heating, p.techs.chp))
                    + sum(m[Symbol("dvThermalProduction"*_n)][t,ts] / p.thermal_cop[t] for t in p.techs.absorption_chiller)
                    - sum(p.space_heating_thermal_load_reduction_with_ghp_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
                    + sum(m[Symbol("dvThermalToSteamTurbine"*_n)][t,ts] for t in p.techs.can_supply_steam_turbine)
                )
            else
                @constraint(m, [ts in p.time_steps_with_grid],
                    sum(m[Symbol("dvThermalProduction"*_n)][t,ts] for t in union(p.techs.heating, p.techs.chp))
                    + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.hot)
                    + sum(p.ghp_heating_thermal_load_served_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
                    ==
                    (p.s.dhw_load.loads_kw[ts] + p.s.space_heating_load.loads_kw[ts])
                    + sum(m[Symbol("dvProductionToWaste"*_n)][t,ts] for t in p.techs.chp)
                    + sum(m[Symbol("dvProductionToStorage"*_n)][b,t,ts] for b in p.s.storage.types.hot, t in union(p.techs.heating, p.techs.chp))
                    + sum(m[Symbol("dvThermalProduction"*_n)][t,ts] / p.thermal_cop[t] for t in p.techs.absorption_chiller)
                    - sum(p.space_heating_thermal_load_reduction_with_ghp_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
                )
            end

        end

        if !isempty(p.techs.cooling)
            
            ##Constraint (5a): Cold thermal loads
            @constraint(m, [ts in p.time_steps_with_grid],
                sum(m[Symbol("dvThermalProduction"*_n)][t,ts] for t in p.techs.cooling)
                + sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.cold)
                + sum(p.ghp_cooling_thermal_load_served_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
                ==
                p.s.cooling_load.loads_kw_thermal[ts]
                + sum(m[Symbol("dvProductionToStorage"*_n)][b,t,ts] for b in p.s.storage.types.cold, t in p.techs.cooling)
                - sum(p.cooling_thermal_load_reduction_with_ghp_kw[g,ts] * m[Symbol("binGHP"*_n)][g] for g in p.ghp_options)
            )
        end
    end
end

function add_hydrogen_load_balance_constraints(m, p; _n="") 
	##Constraint: Hydrogen load can only be served from high pressure storage
    if !isempty(p.s.storage.types.hydrogen_hp)
        @constraint(m, [ts in p.time_steps], 
            sum(m[Symbol("dvDischargeFromStorage"*_n)][b,ts] for b in p.s.storage.types.hydrogen_hp) 
            ==
            p.s.hydrogen_load.loads_kg[ts]
        )
    end
end